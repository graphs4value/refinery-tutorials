class EAttribute extends EStructuralFeature.

class EAnnotation extends EModelElement {
    contains EStringToStringMapEntry[] details
    container EModelElement eModelElement opposite eAnnotations
    contains EObject[] contents
    EObject[] references
}

class EClass extends EClassifier {
    EClass[] eSuperTypes
    contains EOperation[] eOperations opposite eContainingClass
    contains EStructuralFeature[] eStructuralFeatures opposite eContainingClass
    contains EGenericType[] eGenericSuperTypes
}

abstract class EClassifier extends ENamedElement {
    container EPackage ePackage opposite eClassifiers
    contains ETypeParameter[] eTypeParameters
}

class EDataType extends EClassifier.

class EEnum extends EDataType {
    contains EEnumLiteral[] eLiterals opposite eEnum
}

class EEnumLiteral extends ENamedElement {
    container EEnum eEnum opposite eLiterals
}

abstract class EModelElement {
    contains EAnnotation[] eAnnotations opposite eModelElement
}

abstract class ENamedElement extends EModelElement.

class EObject.

class EOperation extends ETypedElement {
    container EClass eContainingClass opposite eOperations
    contains ETypeParameter[] eTypeParameters
    contains EParameter[] eParameters opposite eOperation
    EClassifier[] eExceptions
    contains EGenericType[] eGenericExceptions
}

class EPackage extends ENamedElement {
    contains EClassifier[] eClassifiers opposite ePackage
    contains EPackage[] eSubpackages opposite eSuperPackage
    container EPackage eSuperPackage opposite eSubpackages
}

class EParameter extends ETypedElement {
    container EOperation eOperation opposite eParameters
}

class EReference extends EStructuralFeature {
    EReference eOpposite
    EAttribute[] eKeys
}

abstract class EStructuralFeature extends ETypedElement {
    container EClass eContainingClass opposite eStructuralFeatures
}

abstract class ETypedElement extends ENamedElement.

class EStringToStringMapEntry.

class EGenericType {
    contains EGenericType eUpperBound
    contains EGenericType[] eTypeArguments
    EClassifier[1] eRawType
    contains EGenericType eLowerBound
    ETypeParameter eTypeParameter
    EClassifier eClassifier
}

class ETypeParameter extends ENamedElement {
    contains EGenericType[] eBounds
}

class Model {
    contains EPackage[1..*] packages
}

error loopInInheritance(EClass a) <->
    eSuperTypes+(a, a).

scope node = 100, Model = 1.